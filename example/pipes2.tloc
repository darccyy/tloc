Set chars "━┃┓┏┛┗"
Set background " "
Set colorSpeed 0.2
Set length 100000
Set blackBg FLS
Array canvas $$width $$height Close
Div $canvas:0 2
Flr $$
Set cx $$
Div $canvas:1 2
Flr $$
Set cy $$

Print "Rendering " $canvas:0 "x" $canvas:1 " canvas " $length " times..."

Set screen ""
Set i 0
Label i
  Con $screen $background
  Con $$ "000" >> screen
  Add $i 1 >> i
  Mul $canvas:0 $canvas:1
  If $i < $$; Goto i

Set prevDir 1
Set hue 0

Set frame 0
Label render
  Set dir $prevDir
  Rand
  If $$ = TRU
    Rand
    If $$ = TRU
      Set sign 1
      Rand
      If $$ = TRU
        Set sign 3
      End
      Add $prevDir $sign
      Mod $$ 4 >> dir
    End
  End

  Mul $prevDir 4
  Add $$ $dir
  Array map
    1 3 1 2
    4 0 2 0
    1 5 1 4
    5 0 3 0
  Close
  Set char $map:$$
  Set char $chars:$char

  Add $hue $colorSpeed
  Mod $$ 360 >> hue
  Mul $cy $canvas:0
  Add $$ $cx
  Mul $$ 4 >> pos
  Con $screen:~$pos $char
  Rnd $hue >> h
  If $h < 10
    Con "00" $h >> h
  Elseif $h < 100
    Con "0" $h >> h
  End
  Con $$ $h >> temp
  Add $pos 4
  Con $temp $screen:$$~ >> screen
  Set prevDir $dir

  If $dir = 0
    Sub $cy 1 >> cy
  Elseif $dir = 1
    Add $cx 1 >> cx
  Elseif $dir = 2
    Add $cy 1 >> cy
  Elseif $dir = 3
    Sub $cx 1 >> cx
  End

  Sub $canvas:0 1 >> w
  Sub $canvas:1 1 >> h
  If $cx < 0
    Set cx $w
  Elseif $cx >= $w
    Set cx 0
  End
  If $cy < 0
    Set cy $h
  Elseif $cy >= $h
    Set cy 0
  End

  Add $frame 1 >> frame
  If $frame < $length; Goto render


Set printScreen ""
Set y 0
Label y
  If $y > 0
    Con $printScreen "~n" >> printScreen
  End
  Set x 0
  Label x
    Mul $y $canvas:0
    Add $$ $x
    Mul $$ 4 >> pos

    Add $pos 1 >> first
    Add $pos 4
    Set str $screen:$first~$$
    Set h 0
    Set i 0
    Label i
      Set digit 0
      If     $str:$i = "1"; Set digit 1
      Elseif $str:$i = "2"; Set digit 2
      Elseif $str:$i = "3"; Set digit 3
      Elseif $str:$i = "4"; Set digit 4
      Elseif $str:$i = "5"; Set digit 5
      Elseif $str:$i = "6"; Set digit 6
      Elseif $str:$i = "7"; Set digit 7
      Elseif $str:$i = "8"; Set digit 8
      Elseif $str:$i = "9"; Set digit 9
      End
      Sub 2 $i
      Pow 10 $$
      Mul $$ $digit
      Add $h $$ >> h

      Add $i 1 >> i
      If $i < 4; Goto i

    Div $h 60
    Mod $$ 2
    Sub $$ 1
    If $$ < 0
      Sub 0 $$
    End
    Sub 1 $$ >> n

    Set m 0

    If $h < 60
      Array rgb 1 $n 0 Close
    Elseif $h < 120
      Array rgb $n 1 0 Close
    Elseif $h < 180
      Array rgb 0 1 $n Close
    Elseif $h < 240
      Array rgb 0 $n 1 Close
    Elseif $h < 300
      Array rgb $n 0 1 Close
    Else
      Array rgb 1 0 $n Close
    End

    Add $rgb:0 $m
    Mul $$ 255
    Rnd $$ >> r
    Add $rgb:1 $m
    Mul $$ 255
    Rnd $$ >> g
    Add $rgb:2 $m
    Mul $$ 255
    Rnd $$ >> b

    Set hexChars "0123456789ABCDEF"
    Set code "~x1b[38;2;"
    Add $rgb:0 $m
    Mul $$ 255
    Rnd $$
    Con $code $$ >> code
    Con $code ";" >> code
    Add $rgb:1 $m
    Mul $$ 255
    Rnd $$
    Con $code $$ >> code
    Con $code ";" >> code
    Add $rgb:2 $m
    Mul $$ 255
    Rnd $$
    Con $code $$ >> code
    Con $code "m" >> code

    Set char $screen:$pos
    Con $printScreen $code
    Con $$ $char >> printScreen

    Add $x 1 >> x
    If $x < $canvas:0; Goto x
  Add $y 1 >> y
  If $y < $canvas:1; Goto y

If $blackBg = TRU
  Print "~x1b[48;2;0;0;0m" $printScreen
Else
  Print $printScreen
End
Halt