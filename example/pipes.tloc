Set chars "━┃┓┏┛┗"
Set background " "
Set cursorChars "▲▶▼◀"
Set showCursor TRU
Set waitTime 50
Array canvas $$width $$height Close
Div $canvas:0 2
Flr $$
Set cx $$
Div $canvas:1 2
Flr $$
Set cy $$

Set screen ""
Set i 0
Label i
  Con $screen $background >> screen
  Add $i 1 >> i
  Mul $canvas:0 $canvas:1
  If $i < $$; Goto i

Set prevScreen NUL
Set prevDir 1
Set hue 0

Label render
  Add $hue 10
  Mod $$ 360 >> hue

  Div $hue 60
  Mod $$ 2
  Sub $$ 1
  If $$ < 0
    Sub 0 $$
  End
  Sub 1 $$ >> x

  Set m 0

  If $hue < 60
    Array rgb 1 $x 0 Close
  Elseif $hue < 120
    Array rgb $x 1 0 Close
  Elseif $hue < 180
    Array rgb 0 1 $x Close
  Elseif $hue < 240
    Array rgb 0 $x 1 Close
  Elseif $hue < 300
    Array rgb $x 0 1 Close
  Else
    Array rgb 1 0 $x Close
  End

  Add $rgb:0 $m
  Mul $$ 255
  Rnd $$ >> r
  Add $rgb:1 $m
  Mul $$ 255
  Rnd $$ >> g
  Add $rgb:2 $m
  Mul $$ 255
  Rnd $$ >> b

  Set hexChars "0123456789ABCDEF"
  Set hex "#"

  Div $r 16
  Flr $$
  Con $hex $hexChars:$$ >> hex
  Mod $r 16
  Flr $$
  Con $hex $hexChars:$$ >> hex

  Div $g 16
  Flr $$
  Con $hex $hexChars:$$ >> hex
  Mod $g 16
  Flr $$
  Con $hex $hexChars:$$ >> hex

  Div $b 16
  Flr $$
  Con $hex $hexChars:$$ >> hex
  Mod $b 16
  Flr $$
  Con $hex $hexChars:$$ >> hex

  Set $printfg $hex
  
  Set printScreen ""
  Set y 0
  Label y
    If $y > 0
      Con $printScreen "~n" >> printScreen
    End
    Set x 0
    Label x
      Mul $y $canvas:0
      Add $$ $x
      Set char $screen:$$
      If $x = $cx
        If $y = $cy
          Set char  $cursorChars:$prevDir
        End
      End
      Con $printScreen $char >> printScreen
      Add $x 1 >> x
      If $x < $canvas:0; Goto x
    Add $y 1 >> y
    If $y < $canvas:1; Goto y
    
  If $printScreen != $prevScreen
    Clear
    Print $printScreen
  End
  Set prevScreen $printScreen

  Set dir $prevDir
  Rand
  If $$ = TRU
    Set sign 1
    Rand
    If $$ = TRU
      Set sign 3
    End
    Add $prevDir $sign
    Mod $$ 4 >> dir
  End

  Mul $prevDir 4
  Add $$ $dir
  Array map
    1 3 1 2
    4 0 2 0
    1 5 1 4
    5 0 3 0
  Close
  Set char $map:$$
  Set char $chars:$char

  Mul $cy $canvas:0
  Add $$ $cx >> pos
  Con $screen:~$pos $char >> temp
  Add $pos 1
  Con $temp $screen:$$~ >> screen
  Set prevDir $dir

  If $dir = 0
    Sub $cy 1 >> cy
  Elseif $dir = 1
    Add $cx 1 >> cx
  Elseif $dir = 2
    Add $cy 1 >> cy
  Elseif $dir = 3
    Sub $cx 1 >> cx
  End

  Sub $canvas:0 1 >> w
  Sub $canvas:1 1 >> h
  If $cx < 0
    Set cx $w
  Elseif $cx >= $w
    Set cx 0
  End
  If $cy < 0
    Set cy $h
  Elseif $cy >= $h
    Set cy 0
  End


  Halt $waitTime
  Goto render