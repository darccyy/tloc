IMPORT random.tloc; ::Import file
PRINT "Hangman!";

:: Read file (nouns.txt)
OP CON $$dir "\nouns.txt";
SCAN $$ >> file;
LEN $file >> len;
OP ADD $len .1 >> len;
SET words [];
SET str "";

:: Convert file to array
SET letter .0;
LABEL letter;

  SET char $file:$letter;
  SET none FLS;
  SET none TRU ? EQ $char "~r";
  SET none TRU ? EQ $char "~n"
  OP ADD $letter .1;
  SET none TRU ? EGT $$ $len;

  OP CON $str $char ? NOT $none >> str;
  CONT words $str ? $none;
  SET str "" ? $none;

  OP ADD $letter .1 >> letter;
  GO letter ? LT $letter $len;


:: Main function
LABEL start;

  :: Get random word
  LEN $words;
  OP LOG $$ .2;
  OP CEIL $$ >> len;

  SET _min .0;
  LEN $words;
  SET _max $$;
  GO random;
  LABEL _back;

  SET word $words:$num;
  LEN $word >> wordLen;
  SET string "";
  SET count .7;
  :: PRINT $word; :: Uncomment to cheat!

  :: Create blank text
  SET j .0;
  LABEL j;
    OP CON $string "_" >> string;

    OP ADD $j .1 >> j;
    GO j ? LT $j $wordLen;

  :: One instance of game
  LABEL frame;

    :: Print text
    PRINT $string;
    OP CON "Guesses left: " $count;
    PRINT $$;
    INPUT "Guess: " >> guess;
    PRINT " ";

    :: Check for matches
    SET newStr "";
    SET correct FLS;
    SET k .0;
    LABEL k;
    
      SET char $string:$k;

      SET charCorrect FLS;
      SET charCorrect TRU ? EQ $guess $word:$k;
      SET char $word:$k ? $charCorrect;
      OP CON $newStr $char >> newStr;
      SET correct TRU ? $charCorrect;

      OP ADD $k .1 >> k;
      GO k ? LT $k $wordLen;
    
    SET string $newStr;

    :: Check win / loss
    SET win FLS;
    SET win TRU ? EQ $word $string;
    GO win ? $win;

    OP SUB $count .1;
    SET count $$ ? NOT $correct;
    GO lose ? ELT $count .0;

    :: Continue with game
    GO frame;

DISSOLVE :: This should never be needed as program is recursive

:: Other functions
LABEL win;
  PRINT "You Win!";
  HALT .0.5;
  OP CON "The word was '" $word;
  OP CON $$ "'";
  PRINT $$;
  HALT .0.5;
  PRINT " ";
  GO start;

LABEL lose;
  PRINT "You Lose :(";
  HALT .0.5;
  OP CON "The word was '" $word;
  OP CON $$ "'";
  PRINT $$;
  HALT .0.5;
  PRINT " ";
  GO start;